with enums.flagSet
with tyr.io.System

/**
 * tests correctness of try catch with nested try catch
 *
 * @note  requires manual inspection ensuring that there is at most one call
 *   to throw in `f`
 *
 * @todo 0.8  add a test ensuring that gotos into surrounding handlers respect
 *   polymorphism
 */
enum S <: flagSet {
  Ex, NA, others, none, HEx, HNA, HOthers, HNone, fb, fe
}

class T <: Exception, suppressCompilerMessage["E_OOP_PRIVACY_VIOLATED"] {
  type var state : S = S !! 0

  type def set(v : S) = state = S !! (state | v)

  type def f(ex : exceptions.Throwable) : void {
    set(S.fb)

    try {
      try {
        throw ex
      } catch e {
        // throws in this handlers should all get represented as gotos
        if Exception { set(S.Ex); throw e }
        if exceptions.NullAccess { set(S.NA); throw e }
        else { set(S.others); throw e }
      }
    } catch e {
      if Exception { set(S.HEx); delete e }
      if exceptions.NullAccess { set(S.HNA); delete e }
      else { set(S.HOthers); delete e }
    }

    set(S.fe)
  }

  test "ex" {
    f(new Exception)
    state == S.Ex | S.HEx | S.fb | S.fe
  }
  test "null" {
    f(null)
    state == S.NA | S.HNA | S.fb | S.fe
  }
  test "sub" {
    f(new T)
    state == S.Ex | S.HEx | S.fb | S.fe
  }
  test "other" {
    try {
      f(new exceptions.FatalSegFault)
    } catch e {
      if exceptions.FatalSegFault {
        if (state == S.others | S.HOthers | S.fb)
          System.exit(0)
      }
    }
    false
  }
}
